# fzf-popup: fzfをtmux popupで実行するヘルパー
# Usage: echo "data" | fzf-popup [fzf_options...]
function fzf-popup() {
  if [[ -n "$TMUX" ]]; then
    fzf --tmux center,80%,80% "$@"
  else
    fzf "$@"
  fi
}

# peco
function peco-select-history() {
    local tac
    if which tac > /dev/null; then
        tac="tac"
    else
        tac="tail -r"
    fi
    selected=$(\history -n 1 | \
        eval $tac | \
        fzf-popup --query "$LBUFFER" --preview 'echo {} | sed "s/ \\\\n/\\n/g" | bat --color=always -l sh --style=plain' --preview-window down,30%,wrap | \
        sed s'/ \\\\n/ /g')
    BUFFER=$(echo "${selected}")
    zle redisplay
    CURSOR=$#BUFFER
    zle clear-screen
}
zle -N peco-select-history
bindkey '^r' peco-select-history

function csp () {
  unset CHEATCOLORS
  BUFFER=$( cheat $1 | grep -v '^#' | fzf-popup --query "$LBUFFER" )
  print -z "$BUFFER"
  export CHEATCOLORS=true
}

function tvi () {
  vim $( tree --charset=o -f $1 | fzf-popup | tr -d '\||`|-' )
}

function aliasp () {
  BUFFER=$(alias | fzf-popup --query "$LBUFFER" | awk -F"=" '{print $1}')
  print -z "$BUFFER"
}

function ssh-peco () {
  ssh $(grep --color=no 'Host ' ~/.ssh/config | fzf-popup | awk '{print $2}')
}

function peco-p() {
  selected=$@
  if [ -n "$selected" ]; then
    BUFFER="${BUFFER}${selected}"
    # カーソル位置を末尾に移動
    CURSOR=$#BUFFER
  fi
}

# git branchを選択
function peco-git-branch () {
  local selected_branch_name="$(git branch -a --color=always | fzf-popup | sed 's/\x1b\[[0-9;]*m//g' | tr -d ' ' | tr -d '*')"
  case "$selected_branch_name" in
    *-\>* )
      selected_branch_name="$(echo ${selected_branch_name} | perl -ne 's/^.*->(.*?)\/(.*)$/\2/;print')";;
    remotes* )
      selected_branch_name="$(echo ${selected_branch_name} | perl -ne 's/^.*?remotes\/(.*?)\/(.*)$/\2/;print')";;
  esac
  peco-p ${selected_branch_name}
}
zle -N peco-git-branch
bindkey '^gb' peco-git-branch

# commit hashを選択
function peco-git-hash () {
  local git_hash="$(git log --oneline --branches --pretty='format:%C(yellow)%h %C(green)%cd %C(blue)%an%C(red)%d %C(reset)%s' | fzf-popup | awk '{print $1}')"
  peco-p ${git_hash}
}
zle -N peco-git-hash
bindkey '^gh' peco-git-hash

# プロセスIDを取得
function peco-ps () {
  local process_id="$(ps auxf | fzf-popup --multi --bind 'ctrl-s:toggle+down' | awk 'BEGIN {ORS=" "}; {print $2}')"
  peco-p ${process_id}
}
zle -N peco-ps
bindkey '^gps' peco-ps

# git stのファイルを(複数)選択
function peco-git-files () {
  local git_files="$(git status -sb | fzf-popup --multi --bind 'ctrl-s:toggle+down' --preview '[[ {1} == "??" ]] && bat --color=always --style=numbers {2} || git diff {2} | delta -s -w $FZF_PREVIEW_COLUMNS' --preview-window right,65% --bind 'ctrl-d:preview-half-page-down,ctrl-u:preview-half-page-up' | awk 'BEGIN {ORS=" "}; {print $2}')"
  peco-p ${git_files}
}
zle -N peco-git-files
bindkey '^gf' peco-git-files

# git sash のリストから選択
function peco-git-stash () {
  local stash="$(git stash list --color=always | fzf-popup | awk -F'[ :]' '{print $1}')"
  peco-p ${stash}
}
zle -N peco-git-stash
bindkey '^gs' peco-git-stash

# gh issue のリストから選択
function peco-gh-issue () {
  local issues="$(gh issue list | fzf-popup | awk -F'[ ]' 'BEGIN{FS="\t"}{print $1}')"
  peco-p ${issues}
}
zle -N peco-gh-issue
bindkey '^gi' peco-gh-issue

# gh pr のリストから選択
function peco-gh-pr () {
  local prs="$(gh pr list | fzf-popup | awk -F'[ ]' 'BEGIN{FS="\t"}{print $1}')"
  peco-p ${prs}
}
zle -N peco-gh-pr
bindkey '^gpr' peco-gh-pr

# gh run のリストからIDを選択
function peco-gh-run () {
  local runs="$(gh run list --limit 200 | fzf-popup | awk -F'[ ]' 'BEGIN{FS="\t"}{print $7}')"
  peco-p ${runs}
}
zle -N peco-gh-run
bindkey '^gr' peco-gh-run

# READMEから選択
function peco-file-line () {
  local line="$(cat README.md | fzf-popup)"
  peco-p ${line}
}
zle -N peco-file-line
bindkey '^gm' peco-file-line

# aliasから選択
function peco-alias () {
  local cmd="$(alias | fzf-popup | awk -F'[=]' '{print $1}')"
  peco-p ${cmd}
}
zle -N peco-alias
bindkey '^ga' peco-alias

# rfv
# 1. Search for text in files using Ripgrep
# 2. Interactively restart Ripgrep with reload action
# 3. Open the file in Vim
function rfv() {
RG_PREFIX="rg --column --line-number --no-heading --color=always --smart-case "
INITIAL_QUERY="${*:-}"
fzf --ansi --disabled --query "$INITIAL_QUERY" \
    --bind "start:reload:$RG_PREFIX {q} || true" \
    --bind "change:reload:sleep 0.1; $RG_PREFIX {q} || true" \
    --delimiter : \
    --preview 'bat --color=always {1} --highlight-line {2}' \
    --preview-window 'up,60%,border-bottom,+{2}+3/3,~3' \
    --bind 'enter:become(vim {1} +{2})'
}

