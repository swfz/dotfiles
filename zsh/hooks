autoload -Uz add-zsh-hook

# tmux pane作成時に最後に実行したsshコマンドを実行するためのフック
function tmux_ssh_preexec() {
  local command=$1
  if [[ "$command" = *ssh* ]]; then
    tmux setenv TMUX_SSH_CMD_$(tmux display -p "#I") $command
  fi
}
add-zsh-hook preexec tmux_ssh_preexec

# gitのglobal hookとlocal hookを共存させるためのフック
# Source: https://github.com/swfz/git-hooks
function preexec_git_global_hooks() {
  inside_git_repo="$(git rev-parse --is-inside-work-tree 2>/dev/null)"
  if [ "$inside_git_repo" ]; then
      githooksDir=$(git rev-parse --show-toplevel)"/"$(git config --local core.hooksPath)
      if [ -d "${githooksDir}" ]; then
        git config --local core.hooksPath "~/git-hooks/hooks"
      fi;
  fi
}
add-zsh-hook preexec preexec_git_global_hooks

# ghコマンドのユーザーをディレクトリによって自動切り替えするためのフック
GH_USER_FILE="/tmp/gh_active_user.txt"
echo $DEFAULT_GH_USER > $GH_USER_FILE

function switch_gh_user() {
  expected_user=$DEFAULT_GH_USER
  current_user=$(cat $GH_USER_FILE)

  # 「もし.gitがあり && git configが成功し && EmailがWork用」なら
  # 期待するユーザーをWork用に上書きする
  if git rev-parse --is-inside-work-tree > /dev/null 2>&1 && \
    email=$(git config user.email 2>/dev/null) && \
    [ "$email" = "$WORK_EMAIL" ]; then

    expected_user="$WORK_GH_USER"
  fi

  # 期待するユーザーが現在の状態と異なる場合のみ、switchを実行
  if [[ "$current_user" != "$expected_user" ]]; then
    if gh auth switch --user "$expected_user"; then
      echo $expected_user > $GH_USER_FILE # 成功した場合のみ状態を更新
    fi
  fi
}
add-zsh-hook chpwd switch_gh_user

# 何も入力しなかったときにもgh_switch_userを発火
# ターミナル等が変わった際への対応
function call_switch_gh_user() {
  if [[ $BUFFER = "" ]]; then
    switch_gh_user
  fi
}
add-zsh-hook precmd call_switch_gh_user

